apply plugin: 'scala'
apply plugin: 'idea'

version = '0.1'

jar.archiveName = "zipkin-mesos-${version}.jar"

repositories {
    mavenCentral()
    maven {
        url "http://maven.twttr.com/"
    }
}

buildDir = 'out/gradle'
libsDirName = '../../'

configurations {
    genTracesTestCompile.extendsFrom testCompile
    genTracesTestRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala'] }
    }
    test {
        scala {
            srcDirs = ['src/test/scala']
            exclude '**/GenZipkinTraces.scala'
        }
    }
    genTraces {
        scala {
            srcDirs = ['src/test/scala']
            include '**/GenZipkinTraces.scala'
            compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.genTracesTestCompile
            runtimeClasspath = output + compileClasspath + configurations.genTracesTestRuntime
        }
    }
}

task genTraces(type: Test) {
    description = 'Sends dummy annotation to Kafka span receiver.'
    group = 'verification'
    testClassesDir = sourceSets.genTraces.output.classesDir
    classpath = sourceSets.genTraces.runtimeClasspath
}

allprojects {
    tasks.matching { task -> task.name == "genTraces" }.all {
        outputs.upToDateWhen { false }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:2.10.3"
    compile "org.apache.mesos:mesos:0.21.1"
    compile "com.google.protobuf:protobuf-java:2.5.0"
    compile "log4j:log4j:1.2.16"
    compile "net.sf.jopt-simple:jopt-simple:4.8"
    compile "org.eclipse.jetty:jetty-http:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-io:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-security:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-server:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-servlet:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-util:9.0.4.v20130625"
    compile "javax.servlet:javax.servlet-api:3.0.1"
    compile "com.101tec:zkclient:0.5"
    compile "com.typesafe.play:play-json_2.10:2.3.9"
    compile "com.github.kristofa:brave-core:3.0.0"
    compile "org.apache.thrift:libthrift:0.9.3"
    compile "org.apache.kafka:kafka_2.10:0.8.2.1"

    testCompile 'junit:junit:4.12'
}

jar {
    dependsOn 'test'
    doFirst {
        from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude "*"
            exclude "about_files/*"
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }
    manifest.attributes("Main-Class": "net.elodina.mesos.zipkin.Cli")
}
