apply plugin: 'scala'
apply plugin: 'idea'

version = '0.1'

jar.archiveName = "zipkin-mesos-${version}.jar"

repositories {
    mavenCentral()
    maven {
        url "http://maven.twttr.com/"
    }
}

buildDir = 'out/gradle'
libsDirName = '../../'

configurations {
    pingTestCompile.extendsFrom testCompile
    pingTestRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        scala { srcDirs = ['src/main/scala'] }
    }
    test {
        scala {
            srcDirs = ['src/test/scala']
            exclude '**/KafkaPing.scala'
        }
    }
    ping {
        scala {
            srcDirs = ['src/test/scala']
            include '**/KafkaPing.scala'
        }
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.pingTestCompile
        runtimeClasspath = output + compileClasspath + configurations.pingTestRuntime
    }
}

task ping(type: Test) {
    description = 'Sends dummy annotation to Kafka span receiver.'
    group = 'verification'
    testClassesDir = sourceSets.ping.output.classesDir
    classpath = sourceSets.ping.runtimeClasspath
}

allprojects {
    tasks.matching { task -> task.name == "ping" }.all {
        outputs.upToDateWhen { false }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:2.10.3"
    compile "org.apache.mesos:mesos:0.21.1"
    compile "com.google.protobuf:protobuf-java:2.5.0"
    compile "log4j:log4j:1.2.16"
    compile "net.sf.jopt-simple:jopt-simple:4.8"
    compile "org.eclipse.jetty:jetty-http:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-io:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-security:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-server:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-servlet:9.0.4.v20130625"
    compile "org.eclipse.jetty:jetty-util:9.0.4.v20130625"
    compile "javax.servlet:javax.servlet-api:3.0.1"
    compile "com.101tec:zkclient:0.5"
    compile "com.typesafe.play:play-json_2.10:2.3.9"
    pingTestCompile "org.apache.kafka:kafka_2.11:0.8.2.1"
    pingTestCompile "io.zipkin:zipkin-common:1.14.1"
    pingTestCompile "io.zipkin:zipkin-receiver-kafka:1.14.1"

    testCompile 'junit:junit:4.12'
}

jar {
    dependsOn 'test'
    doFirst {
        from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude "*"
            exclude "about_files/*"
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }
    manifest.attributes("Main-Class": "net.elodina.mesos.zipkin.Cli")
}
